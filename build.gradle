plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.10.6'
}

repositories {
    mavenCentral()
}

dependencies {
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'org.example.App'
}

graalvmNative {
    binaries {
        main {
            quickBuild = true
        }
    }
}

tasks.register('zipNativeOutput') {
    dependsOn tasks.named('nativeCompile')

    doLast {
        def outputDir = layout.buildDirectory.dir("native/nativeCompile").get().asFile

        outputDir.eachFile { file ->
            if (file.isFile()) {
                def zipFile = new File(outputDir, "${file.name}.zip")
                ant.zip(destfile: zipFile) {
                    fileset(file: file)
                }
            }
        }
    }
}

nativeCompile.finalizedBy("zipNativeOutput")
